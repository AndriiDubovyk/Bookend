apply plugin: 'com.android.application'

dependencies {
	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation 'com.google.android.material:material:1.8.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation "androidx.fragment:fragment:1.5.6"

	if (file('../jni/build.gradle').isFile())
		api project(':jni')
	else
		api 'com.artifex.mupdf:fitz:1.20.2'
}

android {
	compileSdkVersion 33
	defaultConfig {
		applicationId "com.andriidubovyk.bookend"
		minSdkVersion 21
		targetSdkVersion 33
		versionName '1.0'
		versionCode 1
	}

	splits {
		abi {
			enable true
			universalApk true
		}
	}

	bundle {
		abi {
			enableSplit true
		}
	}

	if (project.hasProperty('release_storeFile')) {
		signingConfigs {
			release {
				storeFile file(release_storeFile)
				storePassword release_storePassword
				keyAlias release_keyAlias
				keyPassword release_keyPassword
			}
		}
		buildTypes {
			release {
				signingConfig signingConfigs.release
				ndk {
					debugSymbolLevel 'FULL'
				}
			}
		}
	}
}
